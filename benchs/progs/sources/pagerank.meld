
type route input(node, node, float).
type route output(node, node, float).

type linear pagerank(node, float).
type linear neighbor-rank(node, node, float).
type linear start(node).
type linear update(node).
type linear new-neighbor-rank(node, node, float).
type linear sum-ranks(node, float).
type linear used-rank(node, node, float).
type linear new-used-rank(node, node, float).

//priority @cluster @static.

const damping = 0.15.
const bound = str2float(@arg1).
const pages = @world.
const start-pagerank = 1.0 / float(pages).

start(A).

start(A) -o { B, W | !input(A, B, W) | neighbor-rank(A, B, start-pagerank)},
            { B, W | !output(A, B, W) | used-rank(A, B, start-pagerank) },
            pagerank(A, start-pagerank),
            update(A).

new-neighbor-rank(A, B, V),
neighbor-rank(A, B, OldV)
   -o neighbor-rank(A, B, V).

new-used-rank(A, B, V),
used-rank(A, B, OldV)
   -o used-rank(A, B, V).

sum-ranks(A, Acc),
NewRank = damping/float(pages) + (1.0 - damping) * Acc,
pagerank(A, OldRank)
   -o pagerank(A, NewRank),
      {B, W, O, Delta | !output(A, B, W), used-rank(A, B, O), Delta = fabs(O - NewRank) * W |
           used-rank(A, B, O), new-neighbor-rank(B, A, NewRank), (if Delta > bound then update(B) otherwise 1 end)}.

update(A), update(A) -o update(A).

update(A)
   -o [sum => V | B, W, Val | neighbor-rank(A, B, Val), !input(A, B, W), V = Val * W |
            neighbor-rank(A, B, Val), new-used-rank(B, A, Val) | sum-ranks(A, V)].

